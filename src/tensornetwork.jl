using Combinatorics: combinations
using DataGraphs.DataGraphsPartitionedGraphsExt
using DataGraphs: DataGraphs, AbstractDataGraph, DataGraph, edge_data, get_vertices_data,
    vertex_data, vertex_data_type
using Dictionaries: Dictionaries, AbstractDictionary, Indices, dictionary, set!, unset!
using Graphs: AbstractSimpleGraph, has_vertex, rem_edge!, rem_vertex!
using ITensorBase: ITensorBase, AbstractITensor, name, names, nametype, unnamedtype
using NamedGraphs: NamedGraphs, NamedEdge, NamedGraph, decoded_vertex, encoded_graph,
    encoded_vertex, vertextype
using SplitApplyCombine: mapview

struct ITensorNetwork{T, V, I} <: AbstractITensorNetwork{T, V}
    tensors::Dictionary{V, T}
    dimname_vertices::Dictionary{I, Set{V}}
    underlying_graph::NamedGraph{V}
    function ITensorNetwork{T, V, I}(::UndefInitializer, vertices) where {T, V, I}
        tensors = Dictionary{V, T}()
        dimname_vertices = Dictionary{I, Set{V}}()
        underlying_graph = NamedGraph(vertices)
        return new{T, V, I}(tensors, dimname_vertices, underlying_graph)
    end
end

function ITensorNetwork{T}(undef::UndefInitializer, vertices) where {T}
    return ITensorNetwork{T, eltype(vertices)}(undef, vertices)
end

function ITensorNetwork{T, V}(undef::UndefInitializer, vertices) where {T, V}
    return ITensorNetwork{T, V, nametype(T)}(undef, vertices)
end

ITensorNetwork(tensors) = ITensorNetwork{valtype(tensors)}(tensors)
ITensorNetwork{T}(tensors) where {T} = ITensorNetwork{T, keytype(tensors)}(tensors)
function ITensorNetwork{T, V}(tensors) where {T, V}
    I = nametype(T)
    tn = ITensorNetwork{T, V, I}(undef, keys(tensors))
    copyto!(tn, tensors)
    return tn
end

ITensorBase.nametype(tn::ITensorNetwork) = nametype(typeof(tn))
ITensorBase.nametype(::Type{<:ITensorNetwork{T, V, I}}) where {T, V, I} = I

Graphs.vertices(tn::ITensorNetwork) = vertices(tn.underlying_graph)

function NamedGraphs.encoded_vertex(graph::ITensorNetwork, vertex)
    return encoded_vertex(graph.underlying_graph, vertex)
end
function NamedGraphs.decoded_vertex(graph::ITensorNetwork, code::Integer)
    return decoded_vertex(graph.underlying_graph, code)
end

NamedGraphs.encoded_graph(graph::ITensorNetwork) = encoded_graph(graph.underlying_graph)

function Base.copy(tn::ITensorNetwork{T}) where {T}
    tn_dst = ITensorNetwork{T}(undef, vertices(tn))
    copyto!(tn_dst, tn)
    return tn_dst
end

function Graphs.rem_vertex!(tn::ITensorNetwork, vertex)
    has_vertex(tn, vertex) || return false

    tensor = tn.tensors[vertex]

    for name in names(tensor)

        # If `ind` is associated with an edge, remove the edge.
        delete_ind_edge!(tn, name)

        # Delete the vertex from that `ind`s vertex list
        # (this index may still be one incident to one other vertex)
        vertex_list = tn.dimname_vertices[name]
        delete!(vertex_list, vertex)

        # If that index is now no longer associated with any vertices, it was dangling,
        # and that index should be deleted from the keys of reverse index mapping
        isempty(vertex_list) && delete!(tn.dimname_vertices, name)
    end

    delete!(tn.tensors, vertex)

    return rem_vertex!(tn.underlying_graph, vertex)
end

# Internal (unsafe)
function delete_ind_edge!(tn, ind)
    vertex_list = tn.dimname_vertices[ind]

    if length(vertex_list) == 2
        src, dst = vertex_list
        rem_edge!(tn.underlying_graph, src => dst)
    end

    return tn
end

# Internal (unsafe)
function delete_ind_vertex!(tn, ind, vertex)
    vertex_list = tn.dimname_vertices[ind]

    delete!(vertex_list, vertex)
    isempty(vertex_list) && delete!(tn.dimname_vertices, ind)

    return tn
end

tensornetwork(f, vertices) = ITensorNetwork(Dict(v => f(v) for v in vertices))

Graphs.is_directed(::Type{<:ITensorNetwork}) = false

# ====================================== DataGraphs ====================================== #

DataGraphs.is_vertex_assigned(tn::ITensorNetwork, vertex) = isassigned(tn.tensors, vertex)
DataGraphs.is_edge_assigned(::ITensorNetwork, _edge) = false

DataGraphs.get_vertex_data(tn::ITensorNetwork, v) = tn.tensors[v]

function check_input(::typeof(set_vertex_data!), tn, tensor, vertex)
    for name in names(tensor)
        vertices = get(tn.dimname_vertices, name, Set())
        if length(setdiff(vertices, Set([vertex]))) > 1
            throw(
                ArgumentError(
                    "index $name can appear in at most one existing tensor"
                )
            )
        end
    end
    return nothing
end

function DataGraphs.insert_vertex_data!(tn::ITensorNetwork, vertex, tensor)
    check_input(set_vertex_data!, tn, tensor, vertex)
    add_vertex!(tn.underlying_graph, vertex)
    update_tensornetwork_metadata!(tn, vertex, tensor)
    insert!(tn.tensors, vertex, tensor)
    return tn
end

function DataGraphs.set_vertex_data!(tn::ITensorNetwork, tensor, vertex)
    check_input(set_vertex_data!, tn, tensor, vertex)
    update_tensornetwork_metadata!(tn, vertex, tensor)
    set!(tn.tensors, vertex, tensor)
    return tn
end

function update_tensornetwork_metadata!(tn, vertex, tensor)
    oldnames = isassigned(tn, vertex) ? names(tn[vertex]) : Set{nametype(tn)}()
    newnames = names(tensor)

    update_tensornetwork_metadata!(tn, vertex, oldnames, newnames)

    return tn
end

function update_tensornetwork_metadata!(tn, vertex, oldnames, newnames)
    # Only have to deal with the indices that aren't shared.
    for name in symdiff(oldnames, newnames)
        if name in oldnames
            delete_ind_edge!(tn, name)
            delete_ind_vertex!(tn, name, vertex)
            continue
        end

        # Now `name` must be a new index that's not in `oldinds`

        vertex_list = get!(tn.dimname_vertices, name, Set())
        if length(vertex_list) > 1
            throw(
                ArgumentError(
                    "index $name can appear in at most one existing tensor, got $(length(vertex_list))."
                )
            )
        end
        push!(vertex_list, vertex)

        # Add an edge if the index is now shared between two vertices.
        if length(vertex_list) == 2
            src, dst = vertex_list
            add_edge!(tn.underlying_graph, src, dst)
        end
    end

    return tn
end

Dictionaries.isinsertable(::ITensorNetwork) = true

function DataGraphs.underlying_graph_type(type::Type{<:ITensorNetwork{T, V}}) where {T, V}
    return fieldtype(type, :underlying_graph)
end

# Can't add/remove edges from `ITensorNetwork` as graph topology fixed by indices.
Graphs.rem_edge!(::ITensorNetwork, _edge) = false
Graphs.add_edge!(::ITensorNetwork, _edge) = false

# PERF: fast lookup compared to `AbstractITensorNetwork` fallback.
function dimnamevertices(tn::ITensorNetwork, name)
    return get(tn.dimname_vertices, name, Set{vertextype(tn)}())
end

# PERF: fast lookup compared to `AbstractITensorNetwork` fallback.
has_dimname(tn::ITensorNetwork, name) = haskey(tn.dimname_vertices, name)

function NamedGraphs.similar_graph(
        T::Type{<:ITensorNetwork},
        vertices = vertextype(T)[]
    )
    return T(undef, vertices)
end
function NamedGraphs.similar_graph(::ITensorNetwork, VD::Type, vertices)
    return ITensorNetwork{VD}(undef, collect(vertices))
end

function NamedGraphs.convert_vertextype(V::Type, tn_src::ITensorNetwork{T}) where {T}
    tn_dst = ITensorNetwork{eltype(tn_src), V}(undef, vertices(tn_src))
    copyto!(tn_dst, tn_src)
    return tn_dst
end

function NamedGraphs.induced_subgraph_from_vertices(tn::ITensorNetwork, subvertices)
    subgraph = similar_graph(tn, subvertices)
    copyto!(subgraph, tn, subvertices)
    return subgraph, subvertices
end
