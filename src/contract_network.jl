using Base.Broadcast: materialize
using Base: @kwdef
using Dictionaries: Dictionary
using ITensorBase: EvaluationOrderAlgorithm, Greedy, ITensor, Mul, dim, lazy, mulopadd!, names,
    optimize_evaluation_order, substitute, symnamedtensor, unnamed

# `contract_network`
@kwdef struct Exact{Order, OrderAlg}
    order::Order = nothing
    order_alg::OrderAlg = Greedy()
end

function contract_network(alg, tn)
    return throw(ArgumentError("`contract_network` algorithm `$(alg)` not implemented."))
end
function contract_network(tn; alg = Exact())
    return contract_network(alg, tn)
end

# `contract_network(::Exact, ...)`
function get_order(alg::Exact, tn)
    # Allow specifying either an explicit `order` or an `order_alg` to compute one.
    order = if !isnothing(alg.order)
        alg.order
    else
        contraction_order(tn; alg = alg.order_alg)
    end
    # Contraction order may or may not have indices attached, canonicalize the format
    # by attaching indices.
    subs =
        Dict(symnamedtensor(i) => symnamedtensor(i, Tuple(axes(t))) for (i, t) in pairs(tn))
    return substitute(order, subs)
end
# A Gramian enters as its separate layer tensors, so the order can place other operands between
# layers; every operand gets a `(key, layer)` key so all keys share one concrete type.
function split_gramians(tn)
    any(t -> t isa AbstractGramian, tn) || return tn
    pairs_split = [
        (key, layer) => tensor for (key, t) in pairs(tn) for
            (layer, tensor) in (t isa AbstractGramian ? pairs(layertensors(t)) : [:tensor => t])
    ]
    return Dictionary(first.(pairs_split), last.(pairs_split))
end

# Promote the operands to their common type before lowering to the lazy expression, so every lazy
# operand shares one concrete type. Otherwise a network of mixed types (a plain tensor is a trivial
# operator, so mixing operators and plain tensors is the common case) widens the symbolic `Mul`
# container to a `UnionAll` it cannot construct. `promote_type`/`convert` keep an all-plain network
# at the plain type (the promotion is a no-op), so its fast path is unchanged.
function contract_network(alg::Exact, tn)
    tn = split_gramians(tn)
    order = get_order(alg, tn)
    T = mapreduce(typeof, promote_type, tn)
    syms_to_ts = Dict(
        symnamedtensor(i, Tuple(axes(t))) => lazy(convert(T, t)) for (i, t) in pairs(tn)
    )
    tn_expression = substitute(order, syms_to_ts)
    return materialize(tn_expression)
end

# `contraction_order`
function contraction_order end
function contraction_order(tn; alg = Greedy())
    return contraction_order(alg, split_gramians(tn))
end
# Convert the tensor network to a flat symbolic multiplication expression.
struct Flat end
function contraction_order(alg::Flat, tn)
    # Same as: `reduce((a, b) -> *(a, b; flatten = true), syms)`.
    syms = vec([symnamedtensor(i, Tuple(axes(tn[i]))) for i in keys(tn)])
    return lazy(Mul(syms))
end
struct LeftAssociative end
function contraction_order(alg::LeftAssociative, tn)
    return prod(i -> symnamedtensor(i, Tuple(axes(tn[i]))), keys(tn))
end
# Internal implementation shared with the OMEinsumContractionOrders extension.
function _contraction_order(alg, tn)
    s = contraction_order(Flat(), tn)
    return optimize_evaluation_order(s; alg)
end
function contraction_order(alg::EvaluationOrderAlgorithm, tn)
    return _contraction_order(alg, tn)
end

# `prod_tensors!`
# `y = x * xs...` left to right with `alg`, conjugating the operands flagged in `conjlist`.
function prod_tensors!(alg, y, x, xs...; conjlist = falses(length(xs) + 1))
    op(i) = conjlist[i] ? conj : identity
    opx = op(1)
    for (i, m) in enumerate(Base.front(xs))
        labels = Tuple(symdiff(names(x), names(m)))
        dims = map(n -> n in names(x) ? size(x, dim(x, n)) : size(m, dim(m, n)), labels)
        T = promote_type(eltype(x), eltype(m))
        z = ITensor(similar(unnamed(x), T, dims), labels)
        mulopadd!(z, opx, x, op(i + 1), m, true, false; alg)
        x, opx = z, identity
    end
    return mulopadd!(y, opx, x, op(length(xs) + 1), last(xs), true, false; alg)
end
