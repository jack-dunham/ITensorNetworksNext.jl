module AlgorithmsInterfaceExtensions

using AlgorithmsInterface: AlgorithmsInterface as AI

abstract type NestedProblem <: AI.Problem end

# ============================ NestedAlgorithm =============================================

abstract type NestedAlgorithm <: AI.Algorithm end

# Subtypes of `NestedAlgorithm` must override `initialize_subsolve` — it
# returns the `(subproblem, subalgorithm, substate)` tuple that the next
# inner `AI.solve!` call consumes. The default `finalize_substate!` copies
# the substate's iterate back into the parent state; subtypes can override
# when more is required.
function initialize_subsolve(
        problem::AI.Problem, algorithm::AI.Algorithm, state::AI.State
    )
    return throw(MethodError(initialize_subsolve, (problem, algorithm, state)))
end

function finalize_substate!(
        _problem::AI.Problem, _algorithm::AI.Algorithm, state::AI.State,
        _subproblem::AI.Problem, _subalgorithm::AI.Algorithm, substate::AI.State
    )
    state.iterate = substate.iterate
    return state
end

function AI.step!(problem::AI.Problem, algorithm::NestedAlgorithm, state::AI.State)
    subproblem, subalgorithm, substate = initialize_subsolve(problem, algorithm, state)
    AI.solve!(subproblem, subalgorithm, substate)
    finalize_substate!(
        problem, algorithm, state,
        subproblem, subalgorithm, substate
    )
    return state
end

# ============================ NestedState =================================================

# State that wraps an inner `substate` and forwards `:iterate` accesses to it,
# so the inner-loop iterate is shared without duplicating storage on the outer
# state. Subtypes must store the inner state as a field named `substate`.
abstract type NestedState <: AI.State end

# Use `getfield` on the right-hand side so future edits to this forwarder
# can't accidentally recurse through the overload.
function Base.getproperty(state::NestedState, name::Symbol)
    name === :iterate && return getfield(state, :substate).iterate
    return getfield(state, name)
end
function Base.setproperty!(state::NestedState, name::Symbol, value)
    name === :iterate && return (getfield(state, :substate).iterate = value)
    return setfield!(state, name, value)
end
function Base.propertynames(state::NestedState)
    return (fieldnames(typeof(state))..., :iterate)
end

# ============================ SweepAlgorithm ==============================================

# Applies `update` to `iterate` at one schedule entry per iteration.
function update!(update, iterate, problem, entry)
    return throw(MethodError(update!, (update, iterate, problem, entry)))
end

@kwdef struct SweepAlgorithm{Schedule, Update, StoppingCriterion <: AI.StoppingCriterion} <:
    AI.Algorithm
    schedule::Schedule
    update::Update
    stopping_criterion::StoppingCriterion = AI.StopAfterIteration(length(schedule))
end

@kwdef mutable struct SweepState{
        Iterate, StoppingCriterionState <: AI.StoppingCriterionState,
    } <: AI.State
    iterate::Iterate
    iteration::Int = 0
    stopping_criterion_state::StoppingCriterionState
end

function AI.initialize_state(
        problem::AI.Problem, algorithm::SweepAlgorithm; iterate, iteration::Int = 0
    )
    stopping_criterion_state = AI.initialize_state(
        problem, algorithm, algorithm.stopping_criterion; iterate
    )
    return SweepState(; iterate, iteration, stopping_criterion_state)
end

function AI.initialize_state!(
        problem::AI.Problem, algorithm::SweepAlgorithm, state::SweepState;
        iteration::Int = 0
    )
    state.iteration = iteration
    AI.initialize_state!(
        problem, algorithm, algorithm.stopping_criterion, state.stopping_criterion_state
    )
    return state
end

function AI.step!(problem::AI.Problem, algorithm::SweepAlgorithm, state::SweepState)
    update!(
        algorithm.update, state.iterate, problem, algorithm.schedule[state.iteration]
    )
    return state
end

# ============================ IterateUntilConverged =======================================

# Repeats `subalgorithm` on the same problem until `stopping_criterion` is met.
@kwdef struct IterateUntilConverged{
        Subalgorithm <: AI.Algorithm, StoppingCriterion <: AI.StoppingCriterion,
    } <: NestedAlgorithm
    subalgorithm::Subalgorithm
    stopping_criterion::StoppingCriterion
end

@kwdef mutable struct IterateUntilConvergedState{
        Substate <: AI.State, StoppingCriterionState <: AI.StoppingCriterionState,
    } <: NestedState
    substate::Substate
    iteration::Int = 0
    stopping_criterion_state::StoppingCriterionState
end

function AI.initialize_state(
        problem::AI.Problem, algorithm::IterateUntilConverged; iterate, iteration::Int = 0
    )
    substate = AI.initialize_state(problem, algorithm.subalgorithm; iterate)
    stopping_criterion_state = AI.initialize_state(
        problem, algorithm, algorithm.stopping_criterion; iterate
    )
    return IterateUntilConvergedState(; substate, iteration, stopping_criterion_state)
end

# Accepts any `NestedState`, so a state type that adds fields (e.g. for MPI termination)
# needs no methods of its own.
function AI.initialize_state!(
        problem::AI.Problem, algorithm::IterateUntilConverged, state::NestedState;
        iteration::Int = 0
    )
    state.iteration = iteration
    AI.initialize_state!(
        problem, algorithm, algorithm.stopping_criterion, state.stopping_criterion_state
    )
    return state
end

function initialize_subsolve(
        problem::AI.Problem, algorithm::IterateUntilConverged, state::NestedState
    )
    return problem, algorithm.subalgorithm, state.substate
end

# ============================ StopWhenConverged ===========================================

# Stopping criterion that fires once `iterate_diff(iterate, previous_iterate) < tol`.
# Concrete iterate types must supply an `iterate_diff` method.
function iterate_diff(a, b)
    return throw(MethodError(iterate_diff, (a, b)))
end

@kwdef struct StopWhenConverged <: AI.StoppingCriterion
    tol::Float64
end

@kwdef mutable struct StopWhenConvergedState{Iterate} <: AI.StoppingCriterionState
    delta::Float64 = Inf
    at_iteration::Int = -1
    previous_iterate::Iterate
end

function AI.initialize_state(::AI.Problem, ::AI.Algorithm, ::StopWhenConverged; iterate)
    return StopWhenConvergedState(; previous_iterate = copy(iterate))
end

function AI.initialize_state!(
        ::AI.Problem, ::AI.Algorithm, ::StopWhenConverged, st::StopWhenConvergedState
    )
    st.delta = Inf
    return st
end

function AI.is_finished!(
        problem::AI.Problem,
        algorithm::AI.Algorithm,
        state::AI.State,
        c::StopWhenConverged,
        st::StopWhenConvergedState
    )
    iterate = state.iterate
    previous_iterate = st.previous_iterate

    delta = iterate_diff(iterate, previous_iterate)

    st.previous_iterate = copy(iterate)

    # delta = 0 initially, so skip this the first time.
    state.iteration == 0 && return false

    st.delta = delta

    if AI.is_finished(problem, algorithm, state, c, st)
        st.at_iteration = state.iteration
        return true
    end

    return false
end

function AI.is_finished(
        ::AI.Problem,
        ::AI.Algorithm,
        ::AI.State,
        c::StopWhenConverged,
        st::StopWhenConvergedState
    )
    return st.delta < c.tol
end

end
