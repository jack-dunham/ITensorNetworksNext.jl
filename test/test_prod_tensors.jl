using Graphs: edges, vertices
using ITensorBase: Index, NamedTensorOperator, apply, inputnames, name, operator, outputnames,
    setname, state, uniquename
using ITensorNetworksNext: Greedy, ITensorNetwork, absorb_matrices!, linkinds, prod_tensors,
    siteinds, tensornetwork
using NamedGraphs: incident_edges, named_grid
using OMEinsumContractionOrders: ExhaustiveSearch, GreedyMethod, TreeSA
using TensorAlgebra: MatricizeContract, TensorOperationsContract
using TensorOperations: TensorOperations as TO
using Test: @test, @test_throws, @testset

@testset "prod_tensors" begin
    @testset "Contract Vectors of ITensors" begin
        i, j, k = Index(2), Index(2), Index(5)
        A = [1.0 1.0; 0.5 1.0][i, j]
        B = [2.0, 1.0][i]
        C = [5.0, 1.0][j]
        D = [-2.0, 3.0, 4.0, 5.0, 1.0][k]

        ts = [A, B, C, D]
        ABCD_1 = prod_tensors(ts)
        ABCD_2 = prod_tensors(ts, Greedy())
        ABCD_3 = prod_tensors(ts, ExhaustiveSearch())
        ABCD_4 = prod_tensors(ts, GreedyMethod())
        ABCD_5 = prod_tensors(ts, TreeSA())
        @test ABCD_1 == ABCD_2 == ABCD_3
        @test ABCD_1 ≈ ABCD_4
        @test ABCD_1 ≈ ABCD_5
    end

    @testset "Contract One Dimensional Network" begin
        dims = (4, 4)
        g = named_grid(dims)
        l = Dict(e => Index(2) for e in edges(g))
        l = merge(l, Dict(reverse(e) => l[e] for e in edges(g)))
        tn = tensornetwork(vertices(g)) do v
            is = map(e -> l[e], incident_edges(g, v))
            return randn(Tuple(is))
        end

        z1 = prod_tensors(tn)[]
        z2 = prod_tensors(tn, Greedy())[]
        z3 = prod_tensors(tn, ExhaustiveSearch())[]
        z4 = prod_tensors(tn, GreedyMethod())[]
        z5 = prod_tensors(tn, TreeSA())[]

        @test abs(z1 - z2) / abs(z1) <= 1.0e3 * eps(Float64)
        @test abs(z1 - z3) / abs(z1) <= 1.0e3 * eps(Float64)

        @test z1 ≈ z2
        @test z1 ≈ z3
        @test z1 ≈ z4
        @test z1 ≈ z5
    end

    @testset "Contract network with operators" begin
        i, j, k = Index(2), Index(2), Index(2)
        o = operator(randn(2, 2), (i,), (j,))     # output i, input j

        # A network mixing an operator with plain tensors previously threw a `convert`
        # `MethodError`; it now contracts, stays an operator, and matches the binary product.
        t = randn(2, 2)[j, k]
        r = prod_tensors([o, t])
        @test r isa NamedTensorOperator
        @test state(r) ≈ state(o * t)
        @test outputnames(r) == outputnames(o * t)
        @test inputnames(r) == inputnames(o * t)

        # An all-operator network is likewise preserved.
        o2 = operator(randn(2, 2), (j,), (k,))
        r2 = prod_tensors([o, o2])
        @test r2 isa NamedTensorOperator
        @test state(r2) ≈ state(o * o2)

        # A fully-contracted operator network reads out as a scalar via `[]`.
        f = randn(2, 2)[i, j]
        @test prod_tensors([o, f])[] ≈ (o * f)[]

        # An all-plain network is unaffected: it is not promoted to an operator.
        a = randn(2, 2)[i, j]
        b = randn(2, 2)[j, k]
        @test !(prod_tensors([a, b]) isa NamedTensorOperator)

        # Pairing is order-independent: a branching network with a surviving output/input pair
        # matches the binary product under any fold order (greedy contraction included).
        ip, mp, m, x = Index(2), Index(2), Index(2), Index(2)
        op = operator(randn(2, 2, 2, 2), (ip, mp), (i, m))
        u = randn(2, 2)[mp, x]
        w = randn(2, 2)[m, x]
        rb = prod_tensors([op, u, w])
        @test outputnames(rb) == outputnames((op * u) * w) == outputnames(op * (u * w))
        @test inputnames(rb) == inputnames((op * u) * w) == inputnames(op * (u * w))
    end

    @testset "absorb_matrices! matches apply ($alg)" for alg in (
            MatricizeContract(), TensorOperationsContract(),
            TensorOperationsContract(; allocator = TO.BufferAllocator()),
        )
        legs = Index.((2, 3, 4))
        function matrix(elt, i)
            o = setname(i, uniquename(name(i)))
            return operator(randn(elt, (o, i)), (name(o),), (name(i),))
        end
        for n in 0:3
            ψ = randn(legs)
            matrices = [matrix(Float64, legs[k]) for k in 1:n]
            expected = foldl((x, m) -> apply(m, x), matrices; init = copy(ψ))
            @test absorb_matrices!(alg, ψ, matrices...) ≈ expected
        end
        @test_throws ArgumentError absorb_matrices!(
            alg, randn(legs), matrix(ComplexF64, legs[1])
        )
    end
end
